/// Ігноруємо пробіли, табуляцію та нові рядки.
WHITESPACE = _{ " " | "\t" | "\n" }

/// Коректне число — це послідовність ASCII цифр
int = @{ ASCII_DIGIT+ }

/// Оператор додавання
add = { "+" }
/// Оператор віднімання
subtract = { "-" }
/// Оператор множення
multiply = { "*" }
/// Оператор ділення
divide = { "/" }
/// Оператор піднесення до степеня
power = { "^" }

/// primary - це числа або вирази в дужках.
primary = { int | "(" ~ expr ~ ")" }
/// factor - обробляє унарні оператори(унарний мінус)
factor = { (subtract)? ~ power_term }
/// power_term - обробляє піднесення до степеня (право-асоціативне, тобто 2 ^ 3 ^ 2 -> 2 ^ (3 ^ 2))
/// Згортаємо це правило вправо під час побудови AST.
power_term = { primary ~ (power ~ factor)* }
/// expr - обробляє додавання та віднімання (ліво-асоціативне, тобто 10 - 5 + 2 -> (10 - 5) + 2)
expr = { term ~ ((add | subtract) ~ term)* }
/// term - обробляє множення та ділення (ліво-асоціативне)
term = { factor ~ ((multiply | divide) ~ factor)* }
/// expressio` - кореневе правило, expr має покрити весь вхід
expression = { SOI ~ expr ~ EOI }